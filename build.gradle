plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "com.palantir.git-version" version "0.15.0"
    id "com.ssblur.mtp" version "1.0.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.ssblur.mtp"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.19.2+build.3:v2"
    }
}

mtp {
    namespace "redderstone"
    bookId "redstone_guide"
    input "documentation"
    output "generated/src/main/resources"
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "com.palantir.git-version"
    apply plugin: "com.ssblur.mtp"

    archivesBaseName = rootProject.archives_base_name
    def matcher = gitVersion() =~ /^v(\d+\.\d+\.\d+).*/
    matcher.find()
    version = matcher.group(1)
    group = rootProject.maven_group

    repositories {
        mavenLocal()
        mavenCentral()
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
